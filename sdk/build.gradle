apply plugin: 'com.android.library'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-android'
apply plugin: 'maven'
apply plugin: 'signing'

android {
    compileSdkVersion 28
    buildToolsVersion "28.0.3"

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 28
        versionCode 7
        versionName "2.6.4"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        abortOnError false
        disable 'InvalidPackage' // okio
    }

    testOptions {
        unitTests {
            returnDefaultValues true
            includeAndroidResources true
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    android.sourceSets {
        test {
            java.srcDirs += "$projectDir/src/androidTest/java"
            resources.srcDirs += "$projectDir/src/androidTest/resources"
        }
    }
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories.mavenDeployer {
        beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

        if (project.properties.containsKey('ossrhUsername') && project.properties.containsKey('ossrhPassword')) {
            repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2/') {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: 'https://oss.sonatype.org/content/repositories/snapshots') {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }
        }

        pom.project {
            name 'Omise Android SDK'
            description 'Android SDK for Omise API'
            packaging 'aar'
            groupId 'co.omise'
            artifactId 'omise-android'
            version '2.6.5'
            url 'https://www.omise.co'

            scm {
                connection 'scm:git:git://git.github.com/omise/omise-android'
                developerConnection 'scm:git:git://git.github.com/omise/omise-android'
                url 'https://github.com/omise/omise-android'
            }

            licenses {
                license {
                    name 'The MIT License (MIT)'
                    url 'https://opensource.org/licenses/MIT'
                }
            }

            developers {
                developer {
                    id 'chakrit'
                    name 'Chakrit Wichian'
                    email 'chakrit@omise.co'
                }
            }
        }
    }
}

dependencies {
    implementation 'joda-time:joda-time:2.9.2'
    implementation 'com.squareup.okhttp3:okhttp:3.4.1'
    androidTestImplementation 'com.android.support:support-annotations:28.0.0'
    androidTestImplementation 'com.google.guava:guava:19.0'
    compileOnly'io.card:android-sdk:5.4.0'
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.json:json:20171018'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    testImplementation 'org.robolectric:robolectric:4.3'
    testImplementation 'junit:junit:4.12'
    testImplementation 'androidx.test:rules:1.3.0-alpha01'
    testImplementation 'androidx.test:runner:1.3.0-alpha01'
    testImplementation 'androidx.test.espresso:espresso-core:3.3.0-alpha01'
    testImplementation 'com.android.support:support-annotations:28.0.0'
    testImplementation "androidx.test.ext:junit:1.1.1"


    androidTestImplementation 'androidx.test:rules:1.3.0-alpha01'
    androidTestImplementation 'androidx.test:runner:1.3.0-alpha01'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0-alpha01'
    androidTestImplementation 'com.android.support:support-annotations:28.0.0'
    androidTestImplementation "androidx.test.ext:junit:1.1.1"
}

repositories {
    mavenCentral()
}
