apply plugin: 'com.android.library'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-android'
apply plugin: 'maven'
apply plugin: 'signing'

android {
    compileSdkVersion 29
    buildToolsVersion "29"

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 29
        versionCode 7
        versionName "2.6.4"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        abortOnError false
        disable 'InvalidPackage' // okio
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories.mavenDeployer {
        beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

        if (project.properties.containsKey('ossrhUsername') && project.properties.containsKey('ossrhPassword')) {
            repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2/') {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: 'https://oss.sonatype.org/content/repositories/snapshots') {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }
        }

        pom.project {
            name 'Omise Android SDK'
            description 'Android SDK for Omise API'
            packaging 'aar'
            groupId 'co.omise'
            artifactId 'omise-android'
            version '2.6.5'
            url 'https://www.omise.co'

            scm {
                connection 'scm:git:git://git.github.com/omise/omise-android'
                developerConnection 'scm:git:git://git.github.com/omise/omise-android'
                url 'https://github.com/omise/omise-android'
            }

            licenses {
                license {
                    name 'The MIT License (MIT)'
                    url 'https://opensource.org/licenses/MIT'
                }
            }

            developers {
                developer {
                    id 'chakrit'
                    name 'Chakrit Wichian'
                    email 'chakrit@omise.co'
                }
            }
        }
    }
}

dependencies {
    compile 'joda-time:joda-time:2.9.2'
    compile 'com.squareup.okhttp3:okhttp:3.4.1'
    androidTestCompile 'com.android.support:support-annotations:26.1.0'
    androidTestCompile('com.android.support.test.espresso:espresso-core:3.0.1', {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.google.code.findbugs', module: 'jsr305'
    })
    androidTestCompile 'com.google.guava:guava:19.0'
    provided 'io.card:android-sdk:5.4.0'
    testCompile 'junit:junit:4.12'
    testCompile 'org.json:json:20171018'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
}
repositories {
    mavenCentral()
}
