// Top-level build file where you can add configuration options common to all sub-projects/modules.
apply plugin: 'io.github.gradle-nexus.publish-plugin'

// Global group id required for nexus publish
group 'co.omise'

buildscript {
    ext {
        sample_app_version = '1.0'
        sample_app_code_version = 1

        omise_sdk_version = '5.8.0'
        omise_sdk_code_version = 59

        compile_sdk_version = 34
        build_tools_version = '29.0.3'
        min_sdk_version = 21
        target_sdk_version = 34

        omise_threeds_sdk_version = '2.5.2.1'

        kotlin_version = '1.7.10'
        android_plugin_version = '8.2.2'
        appcompat_version = '1.3.0'
        joda_time_version = '2.9.2'
        okhttp_version = '4.9.0'
        material_version = '1.0.0'
        support_annotations_version = '28.0.0'
        guava_version = '19.0'
        jackson_version = '2.11.0'
        preference_version = '1.1.0'
        annotation_version = '1.0.0'

        fragment_testing_version = '1.2.0-alpha01'
        junit_version = '4.12'
        test_version = '1.3.0-alpha01'
        robolectric_version = '4.10.3'
        espresso_version = '3.5.1'
        junit_ext_version = '1.1.1'
        mockito_version = '5.3.1'
        mockito_kotlin_version = '5.0.0'
        constraintlayout_version = '1.1.3'
        coroutines_version = '1.6.4'
        lifecycle_version = '2.2.0'
        arch_version = '2.1.0'
        dexopener_version = '2.0.5'
        jacoco_version = '0.8.10'
        sonarqube_version = '4.3.1.3277'
        uiautomator_version = '2.2.0'
        activity_version = '1.2.4'
    }

    repositories {
        google()
        mavenCentral()
        maven {
            url 'https://maven.google.com/'
            name 'Google'
        }
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        classpath "com.android.tools.build:gradle:$android_plugin_version"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jacoco:org.jacoco.core:$jacoco_version"
        classpath "org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:$sonarqube_version"
        classpath 'org.jlleitschuh.gradle:ktlint-gradle:12.0.3'
        classpath "io.github.gradle-nexus:publish-plugin:2.0.0"

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
        maven {
            url 'https://maven.google.com/'
            name 'Google'
        }
        mavenLocal()
    }
}

tasks.register('clean', Delete) {
    delete rootProject.buildDir
}

nexusPublishing {
    repositories {
        sonatype {
            nexusUrl = uri("https://ossrh-staging-api.central.sonatype.com/service/local/")
            snapshotRepositoryUrl = uri("https://central.sonatype.com/repository/maven-snapshots/")
            username = System.getenv("SONATYPE_USERNAME")
            password = System.getenv("SONATYPE_PASSWORD")
        }
    }
}

// Aggregated Jacoco report for all modules
apply plugin: 'jacoco'

task jacocoRootReport(type: JacocoReport) {
    description = 'Generates an aggregate coverage report from all subprojects'
    group = 'Reporting'
    
    reports {
        xml.required = true
        html.required = true
        html.outputLocation = file("${buildDir}/reports/jacoco/jacocoRootReport/html")
    }
    
    // Explicitly depend on test tasks
    dependsOn ':sdk:testProductionDebugUnitTest'
    dependsOn ':app:testJavaProductionDebugUnitTest'
    dependsOn ':bankapp:testDebugUnitTest'
    
    // Collect source directories from all subprojects
    sourceDirectories.from = files(subprojects.collect { project ->
        "${project.projectDir}/src/main/java"
    })
    
    // Collect class directories from all subprojects
    classDirectories.from = files(subprojects.collect { project ->
        [
            fileTree(
                dir: "${project.buildDir}/tmp/kotlin-classes/productionDebug",
                excludes: [
                    '**/R.class',
                    '**/R$*.class',
                    '**/BuildConfig.*',
                    '**/Manifest*.*',
                    '**/*Test*.*',
                    'android/**/*.*'
                ]
            ),
            fileTree(
                dir: "${project.buildDir}/tmp/kotlin-classes/javaProductionDebug",
                excludes: [
                    '**/R.class',
                    '**/R$*.class',
                    '**/BuildConfig.*',
                    '**/Manifest*.*',
                    '**/*Test*.*',
                    'android/**/*.*'
                ]
            ),
            fileTree(
                dir: "${project.buildDir}/tmp/kotlin-classes/debug",
                excludes: [
                    '**/R.class',
                    '**/R$*.class',
                    '**/BuildConfig.*',
                    '**/Manifest*.*',
                    '**/*Test*.*',
                    'android/**/*.*'
                ]
            )
        ]
    }.flatten())
    
    // Collect execution data from all subprojects
    executionData.from = files(subprojects.collect { project ->
        fileTree(
            dir: project.buildDir,
            includes: ['**/*.exec']
        )
    }.flatten())
}
